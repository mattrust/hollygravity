Camera = {}


Function Camera.p_init(width, height, posx, posy)
    Camera.displaywidth = GetAttribute(#DISPLAY, 1, #ATTRWIDTH)
    Camera.displayheight = GetAttribute(#DISPLAY, 1, #ATTRHEIGHT) - 30
    Camera.width = width
    Camera.height = height
    Camera.offsetx = posx - Camera.displaywidth / 2
    Camera.offsety = posy - Camera.displayheight / 2

    ; ensure camera stays within playfield
    Camera.offsetx = Limit(Camera.offsetx, 0, Camera.width - Camera.displaywidth)
    Camera.offsety = Limit(Camera.offsety, 0, Camera.height - Camera.displayheight)
EndFunction


Function Camera.p_update(posx, posy)
    ; ensure that rocket stays within camera view
    Camera.offsetx = Limit(Camera.offsetx, posx - Camera.displaywidth + 20 , posx - 20)
    Camera.offsety = Limit(Camera.offsety, posy - Camera.displayheight + 20 , posy - 20)

    ; move camera slowly towards rocket
    Local diffx = posx - Camera.offsetx - Camera.displaywidth / 2
    Local diffy = posy - Camera.offsety - Camera.displayheight / 2

    If diffx * diffx + diffy * diffy > 2500
        Local angle = Atan2(diffy, diffx)
        Camera.offsetx = Camera.offsetx + Cos(angle) * 3
        Camera.offsety = Camera.offsety + Sin(angle) * 3
    EndIf

    ; ensure camera stays within playfield
    Camera.offsetx = Limit(Camera.offsetx, 0, Camera.width - Camera.displaywidth)
    Camera.offsety = Limit(Camera.offsety, 0, Camera.height - Camera.displayheight)
EndFunction
