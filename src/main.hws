If GetProgramInfo() = #PRGTYPE_SCRIPT
    DATADIR = "/"
Else
    DATADIR = ""
EndIf


@INCLUDE "rocket.hws"
@INCLUDE "levels.hws"
@INCLUDE "camera.hws"


Function p_game_loop()
    BeginDoubleBuffer
    Repeat
        If IsKeyDown("Esc") Then Return(FALSE)

        Rocket.p_handle_events()

        If Rocket.posx < 0 Or Rocket.posx > Level.width Or 
                Rocket.posy < 0 Or Rocket.posy > Level.height
            ; we've left the universe
            Return(False)
        EndIf

        DebugPrint(Rocket.landed)
        If Abs(Rocket.speedx) < 1 And Abs(Rocket.speedy) < 1 And Rocket.dir > Rad(260) And Rocket.dir < Rad(280)
            If Collision(#BRUSH_VS_BOX, Level.brush, 0, 0, Rocket.posx - 5, Rocket.posy + 10, 10, 3)
                Rocket.landed = True
                Rocket.speedx = 0
                Rocket.speedy = 0
                Rocket.dir = Rad(270)
            EndIf
        EndIf
        DebugPrint(Rocket.landed)

        Local rbrid = Rocket.rocket_brush[Rocket.frame]
        Local rbrw = GetAttribute(#BRUSH, rbrid, #ATTRWIDTH)
        Local rbrh = GetAttribute(#BRUSH, rbrid, #ATTRHEIGHT)
        If Collision(#BRUSH, Level.brush, 0, 0, rbrid, Rocket.posx - rbrw / 2, Rocket.posy - rbrh / 2)
            Return(FALSE)
        EndIf

        Rocket.p_update()
        Camera.p_update(Rocket.posx, Rocket.posy)

        Cls
        Level.p_draw()
        Rocket.p_draw()
        SetFillStyle(#FILLCOLOR)
        Box(0, 450, 800, 30, #GRAY)
        TextOut(20, 460, "Fuel")
        If Rocket.fuel > 1
            Box(60, 460, Rocket.fuel, 10, #RED)
        EndIf

        Flip
        WaitTimer(1, 40)
    Forever
    EndDoubleBuffer
EndFunction


HidePointer

Level.p_init()
Rocket.p_init()
Camera.p_init(Level.width, Level.height, Rocket.posx, Rocket.posy)

StartTimer(1)

If p_game_loop()

Else
    EndDoubleBuffer
    PlayAnim(Rocket.explode_brush, Rocket.posx - Camera.offsetx - 48, Rocket.posy - Camera.offsety - 48, {Speed=7})
    ClearScreen(#RANDOMEFFECT)
EndIf
