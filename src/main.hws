; obtain relative path to images etc.
vers = GetVersion()
If HaveItem({"AmigaOS3", "MorphOS", "WarpOS", "AmigaOS4", "AROS"}, vers.Platform)
    If GetProgramInfo() = #PRGTYPE_SCRIPT
        DATADIR = "/"
    Else
        DATADIR = ""
    EndIf
Else ; Linux etc.
    If GetProgramInfo() = #PRGTYPE_SCRIPT
        DATADIR = "../"
    Else
        DATADIR = ""
    EndIf
EndIf


@INCLUDE "rocket.hws"
@INCLUDE "levels.hws"
@INCLUDE "camera.hws"
@INCLUDE "panel.hws"
@INCLUDE "parcels.hws"

Function p_game_loop()
    BeginDoubleBuffer
    Repeat
        If IsKeyDown("Esc") Then Return(FALSE)
        Local canland = False

        Rocket.p_handle_events

        If Rocket.posx < 0 Or Rocket.posx > Level.width Or 
                Rocket.posy < 0 Or Rocket.posy > Level.height
            ; we've left the universe
            Return(False)
        EndIf

        If Abs(Rocket.speedx) < 1 And Abs(Rocket.speedy) < 1 And Rocket.dir > Rad(260) And Rocket.dir < Rad(280)
            canland = True
            If Collision(#BRUSH_VS_BOX, Level.brush, 0, 0, Rocket.posx - 5, Rocket.posy + 10, 10, 3)
                Rocket.landed = True
                Rocket.speedx = 0
                Rocket.speedy = 0
                Rocket.dir = Rad(270)
            EndIf
        EndIf

        ; pixel exact collision test of rocket and cave
        Local rbrid = Rocket.rocket_brush[Rocket.frame]
        Local rbrw = GetAttribute(#BRUSH, rbrid, #ATTRWIDTH)
        Local rbrh = GetAttribute(#BRUSH, rbrid, #ATTRHEIGHT)
        If Collision(#BRUSH, Level.brush, 0, 0, rbrid, Rocket.posx - rbrw / 2, Rocket.posy - rbrh / 2)
            Return(FALSE)
        EndIf

        If Rocket.landed
            If Rocket.haveparcel
                ; did we land at port?
                If Collision(#BOX, Rocket.posx -10, Rocket.posy - 10, 20, 20, Level.port_posx, Level.port_posy, Level.port_width, Level.port_height)
                    Rocket.haveparcel = False
                EndIf
            Else
                ; we can only load parcel if rocket's store is empty
                If Parcels.p_check()
                    Rocket.haveparcel = True
                EndIf
            EndIf
        EndIf

        Rocket.p_update
        Panel.p_update(Rocket.fuel, canland)
        Camera.p_update(Rocket.posx, Rocket.posy)

        Cls
        Level.p_draw
        Rocket.p_draw
        Parcels.p_draw
        Panel.p_draw

        Flip
        WaitTimer(1, 40)
    Forever
    EndDoubleBuffer
EndFunction


HidePointer

Parcels.p_init
Level.p_init
Rocket.p_init
Panel.p_init
Camera.p_init(Level.width, Level.height, Rocket.posx, Rocket.posy)

StartTimer(1)

If p_game_loop()

Else
    EndDoubleBuffer
    PlayAnim(Rocket.explode_brush, Rocket.posx - Camera.offsetx - 48, Rocket.posy - Camera.offsety - 48, {Speed=7})
    ClearScreen(#RANDOMEFFECT)
EndIf
